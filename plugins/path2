#

package path2;
use HTML::TreeBuilder;

#my $blogimg = "http://www.plover.com/~mjd/pictures/blog";
my $blogimg;

sub start {
  open F, ">", "/tmp/path.err";
  $blogimg = $blosxom::testmode
        ? "http://plover.com/~mjd/pictures/blog"
        : "http://pic.blog.plover.com";
  return 1;
}

sub story {
  my ($pkg, $path, $filename, $story_ref, $title_ref, $body_ref, $base) = @_;

  return unless $$body_ref =~ /\bBLOG(?:IMG)?(?:LINK|REF)\b/;

  print F "*** $path $filename\n";

  my $TB = HTML::TreeBuilder->new();
  my $html = $TB->parse($$body_ref)->eof->elementify();
  my $changed;

  my @changes;
  for my $link ($html->look_down(_tag => 'a')) {
    my $url = $link->attr('href');

    if ($url =~ m{^BLOG(?:LINK|REF)/(.*)}) {
      push @changes, [qq{href="$url"}, qq{href="$blosxom::url/$1"}];
    } elsif ($url =~ m{^BLOGIMG(?:LINK|REF)/(.*)}) {
      my $file = $1;
      if ($file =~ m{/}) {
        push @changes, [qq{href="$url"}, qq{href="$blogimg/$file"}];
      } else {
        push @changes, [qq{href="$url"}, qq{href="$blogimg$path/$filename/$file"}];
      }
    }
  }

  for my $link ($html->look_down(_tag => 'img')) {
    my $url = $link->attr('src');
    if ($url =~ m{^BLOGIMG(?:LINK|REF)/(.*)}) {
      my $file = $1;
      if ($file =~ m{/}) {
        push @changes, [qq{src="$url"}, qq{src="$blogimg/$file"}];
      } else {
        push @changes, [qq{src="$url"}, qq{src="$blogimg$path/$filename/$file"}];
      }
    }
  }

  for my $change (@changes) {
    my ($from, $to) = @$change;
    print F "Replacing [[$from]] with [[$to]]\n";
    my $pos = index($$body_ref, $from);
    if ($pos < 0) {
      print F "!!! Couldn't find target!\n";
      next;
    }
    substr($$body_ref, $pos, length($from), $to);
    print F "  Replaced at position $pos\n";
  }

  return 1;
}

1;
