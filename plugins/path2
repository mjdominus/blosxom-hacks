#

package path2;
use HTML::TreeBuilder;

#my $blogimg = "http://www.plover.com/~mjd/pictures/blog";
my $blogimg;
my $first_image_url;

sub start {
  open F, ">", "/tmp/path.$>";
  $blogimg = $blosxom::testmode
        ? $testmode_image_url
        : $production_image_url;
  die "No blog image URL defined" unless defined $blogimg;
  return 1;
}

sub last {
  undef $first_image_url;
}

sub story {
  my ($pkg, $arg) = @_;
  my ($path, $filename, $story_ref, $title_ref, $body_ref, $base)
    = @{$arg}{qw/category filename storyref titleref bodyref
		 catpath datepath metadata/};

  return unless $$body_ref =~ /\bBLOG(?:IMG)?(?:LINK|REF)\b/;

  print F "*** $path $filename\n";

  my $TB = HTML::TreeBuilder->new();
  my $html = $TB->parse($$body_ref)->eof->elementify();
  my $changed;

  my @changes;
  for my $link ($html->look_down(_tag => 'a')) {
    my $url = $link->attr('href');

    if ($url =~ m{^BLOG(?:LINK|REF)/(.*)}) {
      push @changes, [qq{href="$url"}, qq{href="$blosxom::url/$1"}];
    } elsif ($url =~ m{^BLOGIMG(?:LINK|REF)/(.*)}) {
      my $file = $1;
      if ($file =~ m{/}) {
        push @changes, [qq{href="$url"}, qq{href="$blogimg/$file"}];
      } else {
        push @changes, [qq{href="$url"}, qq{href="$blogimg$path/$filename/$file"}];
      }
    }
  }

  for my $link ($html->look_down(_tag => 'img')) {
    my $url = $link->attr('src');
    if ($url =~ m{^BLOGIMG(?:LINK|REF)/(.*)}) {
      my $file = $1;
      if ($file =~ m{/}) {
        push @changes, [qq{src="$url"}, qq{src="$blogimg/$file"}];
      } else {
        push @changes, [qq{src="$url"}, qq{src="$blogimg$path/$filename/$file"}];
      }
    }
  }

  for my $change (@changes) {
    my ($from, $to) = @$change;
    print F "Replacing [[$from]] with [[$to]]\n";
    if (! defined $first_image_url && $from =~ /src=/) {
      print F "Trying to set first image; to=$to\n";
      ($first_image_url) = ($to =~ /src="(.*)"/);
      print F "first_image_url set to $first_image_url\n"
    }
    my $pos = index($$body_ref, $from);
    if ($pos < 0) {
      print F "!!! Couldn't find target!\n";
      next;
    }
    substr($$body_ref, $pos, length($from), $to);
    print F "  Replaced at position $pos\n";
  }

  return 1;
}

sub foot {
  print F "In foot with first_image = $first_image_url\n";
  $first_image_url ||= "https://pic.blog.plover.com/FIRST_IMAGE_DEFAULT.jpg";
  $blosxom::output =~ s/FIRST_IMAGE_URL/$first_image_url/g;
}

1;
