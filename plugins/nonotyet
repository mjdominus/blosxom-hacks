
package nonotyet;
my $now = time;
my %omit;

sub start { 
    $blosxom::testmode = $ENV{SCRIPT_NAME} =~ /testblog/;
#    $blosxom::show_future_entries = $blosxom::testmode;
    open F, ">", "/tmp/nonotyet.$<";
    print F scalar(localtime), " $$ testmode: $blosxom::testmode\n";
    return 1;
}

use Carp 'carp';
sub convert_date {
  my ($date) = @_;
  print F "Converting date '$date'\n";
  if (! defined $date) {
    carp "Undefined date '$date'";
    return undef;
  } elsif ($date =~ /^\d+$/) {
    return $date; # assumed to already be epoch time
  } elsif (my ($y, $m, $d) = $date =~ /^(\d\d\d\d)-(\d\d)-(\d\d)$/) {
    require Time::Local;
    return Time::Local::timelocal(0,0,0, $d, $m-1, $y-1900);
  } else {
    carp "Unrecognized date '$date'";
    return undef;
  }
}

# Suppress articles with "notyet" files
sub filter {
  my ($pkg, $f) = @_;
  for my $k (keys %$f) {
    (my $notyet = $k) =~ s/\.blog$/\.notyet/;
    (my $file = $k) =~ s{^$blosxom::datadir}{}o;
    $file =~ s/\.\w+$//;
    print F "$file $k $f->{$k}\n";
    my $meta_published = meta::get($file, "published");
    $meta_published = convert_date($meta_published) if defined $meta_published;
    my $has_meta = meta::has($file);
    my $has_notyet = -e $notyet;
    my $publish = 0 + (! $has_notyet && ($has_meta ? $meta_published : 1));

    print F "  has_meta: $has_meta meta_published: $meta_published\n";
    print F "  has_notyet: $has_notyet ($notyet)\n";
    print F "  date database: $f->{$k}\n";
    print F "  WILL PUBLISH: $publish\n";

    if ($f->{$k} > $now + 86400 * 14) {
      # completely suppress articles more than two weeks in the future,
      # even in test mode
      print F "  way too new ($f->{$k})\n";
      delete $f->{$k};
    }  elsif (! $publish || $f->{$k} > $now) {
      $omit{$file} = 1;
      unless ($blosxom::testmode) {
        print F "  and not in test mode\n";
        delete $f->{$k};
      }
    }
  }
  return 1;
}

sub story {
  my ($pkg, $path, $filename, $story_ref, $title_ref, $body_ref) = @_;
  return 1 unless $blosxom::testmode;
  my $q = "$path/$filename";
  $$body_ref = qq{<p><font color="red" size="+1">Will not appear in live blog</font></p>} . $$body_ref
    if $omit{$q};
}

1;

